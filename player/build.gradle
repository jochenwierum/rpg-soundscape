plugins {
    id 'application'
    id 'org.springframework.boot' version '2.4.2'
}

ext {
    version_javacpp = '1.5.4'
    version_ffmpeg = "4.3.1-$version_javacpp"
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation platform(group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: '2.4.2')
    implementation platform(group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.12.1')

    implementation project(':standalone-parser')
    runtimeOnly project(':web')

    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-actuator') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'

    implementation (group: 'com.googlecode.soundlibs', name: 'mp3spi', version: '1.9.5.4') {
        exclude group: 'junit', module: 'junit'
    }
    implementation group: 'com.googlecode.soundlibs', name: 'vorbisspi', version: '1.0.3.3'

    implementation(group: 'org.bytedeco', name: 'ffmpeg-platform', version: "$version_ffmpeg") {
        exclude group: 'org.bytedeco', module: 'ffmpeg'
        exclude group: 'org.bytedeco', module: 'javacpp'
    }
    implementation group: 'org.bytedeco', name: 'ffmpeg', version: "$version_ffmpeg"
    runtime group: 'org.bytedeco', name: 'ffmpeg', version: "$version_ffmpeg", classifier: "linux-x86_64"
    runtime group: 'org.bytedeco', name: 'ffmpeg', version: "$version_ffmpeg", classifier: "linux-armhf"
    runtime group: 'org.bytedeco', name: 'ffmpeg', version: "$version_ffmpeg", classifier: "windows-x86_64"
    runtime group: 'org.bytedeco', name: 'javacpp', version: "$version_javacpp", classifier: "linux-x86_64"
    runtime group: 'org.bytedeco', name: 'javacpp', version: "$version_javacpp", classifier: "linux-armhf"
    runtime group: 'org.bytedeco', name: 'javacpp', version: "$version_javacpp", classifier: "windows-x86_64"

    implementation 'net.dv8tion:JDA:4.2.0_227'

    testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // needed by async log4j logger
    runtime group: 'com.lmax', name: 'disruptor', version: '3.4.2'
}

test {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

jar {
    enabled = true
}

application {
    mainClassName = 'de.jowisoftware.rpgsoundscape.player.Main'
    applicationDefaultJvmArgs = ['--enable-preview']
}
tasks.bootRun.workingDir "${project.projectDir}/.."
