plugins {
    id "com.github.node-gradle.node" version "3.0.0"
}

apply plugin: 'base'

node {
    version = '14.15.4'
    download = true
}

task buildApp(type: NpmTask) {
    dependsOn npmInstall
    args = ['run', 'build']

    inputs.files fileTree("public")
    inputs.files fileTree("src")

    inputs.file 'package.json'
    inputs.file 'babel.config.js'
    inputs.file 'package-lock.json'
    inputs.file 'postcss.config.js'
    inputs.file 'tailwind.config.js'
    inputs.file 'vue.config.js'

    outputs.dir "${buildDir}/webpack"
}

jar {
    dependsOn buildApp

    from ("${buildDir}/webpack") {
        into("static")
    }
    includeEmptyDirs = true
}

String testsExecutedMarkerName = "${buildDir}/.tests.executed"

task npmTest(type: NpmTask) {
    dependsOn assemble

    args = ['run', 'test']

    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'babel.config.js'
    inputs.file 'package-lock.json'
    inputs.file 'postcss.config.js'
    inputs.file 'vue.config.js'

    // allows easy triggering re-tests
    doLast {
        new File(testsExecutedMarkerName).text = 'delete this file to force re-execution JavaScript tests'
    }

    outputs.file testsExecutedMarkerName
}

check.dependsOn npmTest

clean {
    delete testsExecutedMarkerName
}
