Include "_animals.soundscape";
Include "_people.soundscape";
Include "_common.soundscape";

includable soundscape _forest_meta {
    categorized as "forest" in "environment";
    categorized as "peaceful" in "mood";
    categorized as "jochen" in "author";
}

includable soundscape _forest_base {
    include soundscape _forest_meta;
    categorized as "travel" in "type";

    looping track birds_1 {
        play sample birds_long1 with amplification of 20%;
    }

    looping track birds_2 {
        sleep between 15s and 30s;
        play sample eagle with amplification of -10%;
    }

    looping track creek {
        play sample creek with amplification of -70%;
    }
}

soundscape "forest by food" {
    described as "Traveling a forest by foot";
    include soundscape _forest_base;

    looping track steps {
        play sample steps_leaves;
    }
}

soundscape "forest by horse" {
    described as "Traveling a forest by horse";

    include soundscape _forest_base;

    looping paused track horse_gallop {
        play sample horse_gallop with amplification of -50%;
    }

    looping track horse_walking {
        play sample horse_walking with amplification of -50%;
    }

    looping track horses_sounds {
        sleep between 30s and 2m;
        randomly {
            weighted with 1 play sample horse_breathing;
            weighted with 1 play sample horse_whinnying;
        }
    }
}

soundscape "forest night camp" {
    described as "Night camp in a forest";
    categorized as "location" in "type";

    include soundscape _forest_meta;

    looping track creek {
        play sample creek with amplification of -80%;
    }

    looping track campfire {
        play sample bonfire with amplification of 150%;
    }

    looping track birds {
        sleep between 30s and 2m;
        play sample bird_night_1;
    }

    looping track owls {
        sleep between 10s and 1m;
        randomly {
            weighted with 1 play sample owl1;
            weighted with 1 play sample owl2;
            weighted with 1 play sample owl3;
        }
    }

    looping track grasshoppers {
        play sample grasshoppers;
    }
}
